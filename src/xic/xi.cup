package xic;

import java_cup.runtime.*;

   
/* Parser code to change the way the parser reports errors (include
   line and column number of the error). */
parser code {:
    
    /* Change the method report_error so it will display the line and
       column of where the error occurred in the input as well as the
       reason for the error which is passed into the method in the
       String 'message'. */
    public void report_error(String message, Object info) {
   
        /* Create a StringBuilder called 'm' with the string 'Error' in it. */
        StringBuilder m = new StringBuilder("Error");
   
        /* Check if the information passed to the method is the same
           type as the type java_cup.runtime.Symbol. */
        if (info instanceof java_cup.runtime.Symbol) {
            /* Declare a java_cup.runtime.Symbol object 's' with the
               information in the object info that is being typecasted
               as a java_cup.runtime.Symbol object. */
            java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) info);
   
            /* Check if the line number in the input is greater or
               equal to zero. */
            if (s.left >= 0) {                
                /* Add to the end of the StringBuilder error message
                   the line number of the error in the input. */
                m.append(" in line "+(s.left+1));   
                /* Check if the column number in the input is greater
                   or equal to zero. */
                if (s.right >= 0)                    
                    /* Add to the end of the StringBuilder error message
                       the column number of the error in the input. */
                    m.append(", column "+(s.right+1));
            }
        }
   
        /* Add to the end of the StringBuilder error message created in
           this method the message that was passed into this method. */
        m.append(" : "+message);
   
        /* Print the contents of the StringBuilder 'm', which contains
           an error message, out on a line. */
        System.err.println(m);
    }
   
    /* Change the method report_fatal_error so when it reports a fatal
       error it will display the line and column number of where the
       fatal error occurred in the input as well as the reason for the
       fatal error which is passed into the method in the object
       'message' and then exit.*/
    public void report_fatal_error(String message, Object info) {
        report_error(message, info);
        System.exit(1);
    }
:};
   

   
/* ------------Declaration of Terminals and Non Terminals Section----------- */
   
/* Terminals (tokens returned by the scanner).  

   Terminals that have no value are listed first and then terminals
   that do have an value, in this case an integer value, are listed on
   the next line down. */
terminal USE, IF, ELSE, WHILE, RETURN, LENGTH; 
terminal TRUE, FALSE, INT, BOOL;
terminal SEMI, COLON, COMMA, UNDERSCORE;
terminal PLUS, MINUS, MULTIPLY, DIVIDE, MOD;
terminal AND, OR, NEGATE;
terminal EQ, LT, GT, EQEQ, LEQ, GEQ, NEQ;
terminal LPAREN, RPAREN, LBRACE, RBRACE, LBRACKET, RBRACKET;

terminal String ID, STRING, CHARACTER;
terminal Integer INTEGER;

/* Non terminals used in the grammar section.  

   Non terminals that have an object value are listed first and then
   non terminals that have an integer value are listed.  An object
   value means that it can be any type, it isn't set to a specific
   type.  So it could be an Integer or a String or whatever. */
non terminal SExp program, expr_list, expr, block_expr_list, use_stmt_list, use_stmt, func_list, func;
non terminal SExp decl_list, decl, type_list, type;   
non terminal SExp expr_comma_list, array;      
/*
  Precedence of non terminals could be defined here.  If you do define
  precedence here you won't need to worry about precedence in the
  Grammar Section, i.e. that TIMES should have a higher precedence
  than PLUS.
  
  The precedence defined here would look something like this where the
  lower line always will have higher precedence than the line before it.
   
  precedence left PLUS, MINUS; 
  precedence left TIMES, DIVIDE; 
*/

  precedence nonassoc ELSE;

  precedence left EQ, LT, GT, LEQ, GEQ;
  precedence left PLUS, MINUS;
  precedence left MULTIPLY, DIVIDE, MOD; 


/* ----------------------------Grammar Section-------------------- */
   
/* The grammar for our parser.
   
   program ::= use_stmt_list expr_list

*/
start with program;

program ::= use_stmt_list:ul func_list:funcl
            {: RESULT = new ListSExp(ul, funcl); :};

use_stmt_list ::= use_stmt_list:ul use_stmt:u
                  {: ((ListSExp) ul).add(u); RESULT = ul; :}
                  |
                  {: RESULT = new ListSExp(); :}
                  ;

use_stmt ::= USE ID:x
             {: RESULT = new ListSExp("use", new AtomSExp(x)); :};

block_expr_list ::= LBRACE expr_list:el RBRACE
                    {: RESULT = el; :}
                    ;

expr_comma_list ::= expr_comma_list:ecl COMMA expr:e
                    {: ((ListSExp) ecl).add(e); RESULT = ecl; :}
                    | 
                    expr:e
                    {: RESULT = new ListSExp(e); :}
                    |
                    {: RESULT = new ListSExp(); :}
                    ;

expr_list ::= expr_list:el expr:e
              {: ((ListSExp) el).add(e); RESULT = el; :}
              |
              {: RESULT = new ListSExp(); :}
              ;

expr ::= expr:e1 PLUS expr:e2
         {: RESULT = new ListSExp("+", e1, e2); :}
         |
         expr:e1 MINUS expr:e2
         {: RESULT = new ListSExp("-", e1, e2); :}
         |
         expr:e1 MULTIPLY expr:e2
         {: RESULT = new ListSExp("*", e1, e2); :}
         |
         expr:e1 DIVIDE expr:e2
         {: RESULT = new ListSExp("/", e1, e2); :}
         |
         expr:e1 MOD expr:e2
         {: RESULT = new ListSExp("%", e1, e2); :}
         |         
         ID:x EQ expr:e  
         {: RESULT = new ListSExp("=", new AtomSExp(x), e); :}
         |
         decl:d EQ expr:e  
         {: RESULT = new ListSExp("=", d, e); :}
         |
         IF LPAREN expr:predicate RPAREN LBRACE expr_list:consequent RBRACE ELSE LBRACE expr_list:alternative RBRACE
         {: RESULT = new ListSExp("if", predicate, consequent, alternative); :}
         | 
         IF LPAREN expr:predicate RPAREN LBRACE expr_list:consequent RBRACE
         {: RESULT = new ListSExp("if", predicate, consequent); :}
         |
         expr:x SEMI
         {: RESULT = x; :}
         |
         INTEGER:n 
         {: RESULT = new AtomSExp(Integer.toString(n)); :}
         |
         LPAREN expr_comma_list:ecl RPAREN
         {: RESULT = ecl; :}
         |
         ID:x
         {: RESULT = new AtomSExp(x); :}
         |
         STRING:s
         {: RESULT = new AtomSExp('"' + s + '"'); :}
         |
         CHARACTER:c
         {: RESULT = new AtomSExp("'" + c + "'"); :}
         ;

decl_list ::= decl_list:dl COMMA decl:d 
	          {: ((ListSExp) dl).add(d); RESULT = dl; :}
	          |
	          decl:d 
              {: RESULT = new ListSExp(d); :}
              |
	          {: RESULT = new ListSExp(); :}
	          ;

decl ::= ID:x COLON type:t
         {: RESULT = new ListSExp(new AtomSExp(x), t); :}       
         ;

type_list ::= type_list:tl COMMA type:t
              {: ((ListSExp) tl).add(t); RESULT = tl; :}
              |
              type:t 
              {: RESULT = new ListSExp(t); :}
              |
              {: RESULT = new ListSExp(); :}
              ;

type ::= INT 
         {: RESULT = new AtomSExp("int"); :}
		 |
		 BOOL
		 {: RESULT = new AtomSExp("bool"); :}
		 |
		 INT LBRACKET RBRACKET
		 {: RESULT = new ListSExp(new AtomSExp("[]"), new AtomSExp("int")); :}
		 |
		 INT LBRACKET expr:e RBRACKET
		 {: RESULT =  new ListSExp(new AtomSExp("[]"), e); :}
		 |
		 INT LBRACKET RBRACKET LBRACKET RBRACKET
		 {: RESULT =  new ListSExp(new AtomSExp("[]"), new ListSExp(new AtomSExp("[]"), new AtomSExp("int"))); :}
         |
         INT LBRACKET RBRACKET LBRACKET expr:e RBRACKET
         {: RESULT =  new ListSExp(new AtomSExp("[]"), new ListSExp(new AtomSExp("[]"), new AtomSExp("int"), e)); :}		 
         |
         INT LBRACKET expr:e RBRACKET LBRACKET RBRACKET
         {: RESULT =  new ListSExp(new AtomSExp("[]"), new ListSExp(new AtomSExp("[]"), new AtomSExp("int")), e); :}
         |
         INT LBRACKET expr:e1 RBRACKET LBRACKET expr:e2 RBRACKET
         {: RESULT =  new ListSExp(new AtomSExp("[]"), new ListSExp(new AtomSExp("[]"), new AtomSExp("int"), e2), e1); :}
         ;

func_list ::= func_list:fl func:f
              {: ((ListSExp) fl).add(f); RESULT = fl; :}
              | 
              {: RESULT = new ListSExp(); :}
              ;        

func ::= ID:x LPAREN decl_list:dl RPAREN block_expr_list:bel
         {: RESULT = new ListSExp(new AtomSExp(x), dl, new ListSExp(), bel); :}
         |
         ID:x LPAREN decl_list:dl RPAREN COLON LPAREN type_list:tl RPAREN block_expr_list:bel
         {: RESULT = new ListSExp(new AtomSExp(x), dl, tl, bel); :}
         ;

         